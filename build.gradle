plugins {
    id 'org.springframework.boot' version '2.1.9.RELEASE'
    id 'io.spring.dependency-management' version '1.0.8.RELEASE'
    id 'java'
}

defaultTasks 'build'

configurations {
    developmentOnly
    runtimeClasspath {
        extendsFrom developmentOnly
    }
}

// don't add unbuilt files to the resources folder
sourceSets {
    main {
        resources {
            exclude '**/*.scss'
            exclude '**/*.ts'
        }
    }
}

version = '0.0.1-SNAPSHOT'
sourceCompatibility = '1.8'

repositories {
    mavenCentral()
}

task webpack(type: Exec) {
    inputs.file 'package.json'
    inputs.file('yarn.lock')
    inputs.file('webpack.config.js')

    inputs.dir('src/main/resources/public')
    outputs.dir('build/resources/main/public')

    commandLine = [ 'yarn', 'build' ]
}

processResources.dependsOn(webpack)

dependencies {
    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'org.springframework.boot:spring-boot-starter-freemarker'
    developmentOnly('org.springframework.boot:spring-boot-devtools')
    implementation 'org.springframework.boot:spring-boot-starter'
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    compileOnly 'org.projectlombok:lombok:1.18.22'
    annotationProcessor 'org.projectlombok:lombok:1.18.22'
}
